plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def globalConfiguration = rootProject.extensions.getByName("ext")

android {
    compileSdkVersion globalConfiguration["compileSdkVersion"]
    buildToolsVersion globalConfiguration["buildToolsVersion"]

    defaultConfig {
        applicationId "com.ozgursakizli.simplechat"
        minSdkVersion hasProperty('minSdk') ? minSdk.toInteger() : globalConfiguration["minSdkVersion"]
        targetSdkVersion globalConfiguration["targetSdkVersion"]
        versionCode globalConfiguration["appVersionCode"]
        versionName globalConfiguration["appVersionName"]
        vectorDrawables.useSupportLibrary = true
        buildFeatures {
            viewBinding true
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", '"https://jsonblob.com/api/"'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", '"https://jsonblob.com/api/"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    def appDependencies = rootProject.ext.appDependencies
    def testDependencies = rootProject.ext.testDependencies
    // Android
    implementation appDependencies.corektx
    implementation appDependencies.appcompat
    implementation appDependencies.activity
    implementation appDependencies.constraintlayout
    implementation appDependencies.lifecycleViewModel
    implementation appDependencies.lifecycleLiveData
    // Google
    implementation appDependencies.googleMaterial
    // Koin
    implementation appDependencies.koinCore
    implementation appDependencies.koinAndroid
    implementation appDependencies.koinViewModel
    // Retrofit
    implementation appDependencies.squareupretrofit
    implementation appDependencies.squareupConverterGson
    implementation appDependencies.squareupOkHttp
    implementation appDependencies.squareupLoggingInterceptor
    // Glide
    implementation appDependencies.glide
    kapt appDependencies.glideCompiler
    // Test Libraries
    testImplementation testDependencies.jUnit
    androidTestImplementation testDependencies.jUnitExt
    androidTestImplementation testDependencies.espressoCore
}